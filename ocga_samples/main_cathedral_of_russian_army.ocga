#OCGA rules example
#Main Cathedral of the Russian Armed Forces
#(c) Zkir 2020

ocga 0.1

rule building:

    # align local coordinates so that X matches the longest dimension, and oriented east
    # align_scope geometry
    # align_scope x_to_longer_side 
    rotate_scope 10.52

    colour  &103010 # should rather be #103010
    material plaster

    # we will start from the rectangle and will rebuild the form
    # basing of this very algorithm
    outer_rectangle mass_model

rule mass_model:
    #print scope_sy
    split_x  ~1: belltower_block | scope_sy+5: main_block_pre   

rule belltower_block:
    scale '1 , scope_sx, 70 
    create_roof onion, 12 
    roof_colour gold 
    roof_material metal 
    split_z  ~0.5: belltower_layer3_pre |
             ~1.4: belltower_layer2_pre |
             ~0.4: belltower_layer1a |
             ~1.4: belltower_layer1 
                                

rule belltower_layer1:
    nope

rule belltower_layer1a:
    bevel 2

rule belltower_layer2_pre:
    scale '0.8, '0.8 
    split_x  ~1:belltower_layer2_portico_pre | ~6: belltower_layer2_x | ~1: belltower_layer2_portico_pre  

rule belltower_layer2_x:
    split_y  ~1: belltower_layer2_portico | ~6: belltower_layer2 | ~1: belltower_layer2_portico  

rule belltower_layer2:
    bevel 2.5

rule belltower_layer2_portico_pre:
    split_y ~1: NIL |  ~6: belltower_layer2_portico |  ~1: NIL  

rule belltower_layer2_portico:
    align_scope x_to_longer_side
    scale scope_sx-2, '1.2, '0.8 
    create_roof gabled, 2 
    tag roof:orientation, across 

rule belltower_layer3_pre:
    scale '0.5, '0.5 
    primitive_cylinder '1 
    create_roof dome, scope_sx/2  # onion
    comp_roof belltower_layer3a 

rule belltower_layer3a:
    translate 0, 0, scope_sx/3.5 
    scale '0.8, '0.8, '1 
    #create_roof pyramidal, scope_sx/2   # onion
    tag roof:shape, pyramidal 

rule main_block_pre:
    split_x  ~1:west_apse_block |  ~4: main_block |  ~1:apse_block  

rule apse_block:
    scale '1, '0.4 
    if relative_Ox < 0 then
        rotate_scope 180
    endif     
    split_z    ~2.75: NIL |
               ~1.25: apse_2_pre |
               ~1: apse_1_pre 
                                

rule apse_1_pre:
    scale '1, '1, scope_sz+5 
    create_roof half_dome, 5 
    roof_material glass 
    roof_colour gray 
    primitive_halfcylinder '1 

rule apse_2_pre:
    translate '-0.38, 0 
    scale '0.7, '0.7 #'0.605
    create_roof half_dome, 6 
    roof_colour gold 
    roof_material metal 
    primitive_halfcylinder '1 
    #scale '1, '0.75 #0.605 

rule west_apse_block:
    scale '1, '0.4 
    if relative_Ox < 0 then
        rotate_scope 180 
    endif 
        
    split_z ~2.75: NIL |
            ~1.25: apse_2_pre |
            ~1: west_apse_1_pre 
                                 

rule west_apse_1_pre:
    scale '1, '1, scope_sz+5 
    create_roof half_dome, 5 
    
    roof_material glass 
    roof_colour gray 
    bevel 6, [1, 2] 

rule main_block:
    split_y  ~0.4: NIL | ~0.6: entrance |  ~4: cube |  ~0.6: entrance | ~0.4: NIL  

rule entrance:
    scale '0.3, '1,18 
    create_roof round, 6 
    if scope_sx  > scope_sy then 
        tag roof:orientation, across 
    else
        tag roof:orientation, along 
    endif

rule cube:
    split_z  ~4: cube2 | ~1: cube1  

rule cube1:
    scale '1, '1, scope_sz  + 5 
    bevel 6 
    create_roof dome, 5 
    roof_material glass 
    roof_colour gray 


rule cube2:
    scale '0.8, '0.8 
    split_x  ~1: sideL |  ~1.73: cube3 |  ~1: sideR  


rule sideL:
    split_y  ~1: side_head_L1 |  ~1.73: side_erkerL |  ~1: side_head_L2  

rule sideR:
    split_y  ~1: side_head_R2 |  ~1.73: side_erkerR |  ~1: side_head_R1  

rule cube3:
    split_y  ~1: side_erkerF |  ~1.73: main_head |  ~1: side_erkerB  

rule main_head:
    create_roof  onion, 12 # onion
    roof_colour gold
    roof_material metal
    split_z  1: side_head_onion_base_pre |  ~1: main_head_layer2_pre |  29: main_head_layer1  

rule main_head_layer2_pre:
    scale '0.8, '0.8
    rotate_scope 22.5 
    primitive_cylinder '1, 8

rule side_head_L1:
    continue side_head  

rule side_head_L2:
    rotate_scope -90
    continue side_head  

rule side_head_R1:
    rotate_scope 180
    continue side_head  

rule side_head_R2:
    rotate_scope -270
    continue side_head  

rule side_head:
    scale '1, '1, '0.75 
    create_roof onion,6  # onion
    roof_colour gold 
    roof_material metal 
    split_z 1: side_head_onion_base_pre |
            ~1: side_head_layer2 |
            22: side_head_layer1 
                                

rule side_head_layer1:
    bevel 2.5, [0] 

rule side_head_layer2:
    scale '0.9, '0.9
    comp_edges 2, side_head_portico
    restore 
    scale '0.7, '0.7 

    
rule side_head_portico:
    scale '0.7, '1, '1
    create_roof gabled, 2 
    tag roof:orientation, across     

rule side_head_onion_base_pre:
    primitive_cylinder '1 
    scale '0.8, '0.8 
    create_roof dome, scope_sx /2   # onion
    comp_roof side_head_onion_base2 

rule side_head_onion_base2:
    translate 0, 0, scope_sx  / 3.5 
    scale '0.8, '0.8,'1 
    tag roof:shape, pyramidal  # onion

rule side_erkerL:
    rotate_scope 0 
    continue side_erker  
    
rule side_erkerR:
    rotate_scope 180 
    continue side_erker  
    
rule side_erkerF:
    rotate_scope 90 
    continue side_erker
    
rule side_erkerB:
    rotate_scope -90 
    continue side_erker  

rule side_erker:
    scale '1, '1, '0.4 
    create_roof gabled, 5 
    tag roof:orientation, across 

    scale scope_sx +6.10, '1 
    translate -6.10/2, 0 
    bevel 3.5, [0,3] 
    comp_roof side_erker_dome 

rule side_erker_dome:
    tag roof:shape, half_dome
    scale '0.9, '0.9, 7 
    
rule main_head_layer1:
    nope    

rule NIL:
    nil

