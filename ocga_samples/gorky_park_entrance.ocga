#Set of rules to generate some geometry for buildings,
#first of all Gorky Park Entrance
# (c)  Zkir 2020, 2024

ocga 0.1

# ===========================================================================================================
# Entrance to the Gorky Park
# ===========================================================================================================
rule building:
#elif getTag  building   ==  triumphal_arch  and getTag 
#         building:architecture   ==  stalinist_neoclassicism:

    #if getTag  building:levels   !=    and getTag  height   == 0:
    #    tag  height, str float getTag  building:levels* 4  
 
    tag height, 21.6
    #tag building, yes 

    # align local coordinates so that X matches the longest dimension
    align_scope geometry 
    align_scope x_to_longer_side
    outer_rectangle building_outline
  

rule building_outline:
    # we need to do offset inside, to compensate cornice, which is huge
    scale scope_sx-2.5, scope_sy-2.5 
    split_x    ~0.06 :  side_column_block  |  ~1 :  pylon_pre  |   ~3.5 :  arch  |   ~1 :  pylon_pre  |  ~0.06 :  side_column_block    

rule arch:
    scale scope_sx, scope_sy-2.0 
    split_z  
             ~1.0:  NIL  |
             ~1.5:  pylon_middle  |  # arch_top_pre
             ~4.0:  arch_columns  |
              0.2:  stilobate  


rule pylon_pre:
    split_z
             ~1.0:  pylon_top |
             ~1.5:  pylon_middle |
             ~4.0:  pylon |
             0.2:  stilobate  
                                 

rule side_column_block:
    rotate_scope 90 
    split_x    ~1:  NIL |  ~1:  side_column_pre |   ~1.5:  NIL |   ~1:  side_column_pre |   ~1:  NIL    

rule side_column_pre:
    split_z 
             ~1.0:  NIL |
             ~1.5:  pylon_middle |
             ~4.0:  pylon |
              0.2:  stilobate 


rule pylon_middle:
    split_z   
            0.3:  cornice4 |
            0.3:  cornice3 |
            ~2:   pylon_middle_wall |
            0.6:  cornice2 |
            0.6:  cornice1 |
            ~1:   pylon_middle_wall    

rule cornice1:
    scale scope_sx+1.4, scope_sy+1.4 
 
rule cornice2:
    scale scope_sx+2.45, scope_sy+2.45 

rule cornice3:
    scale scope_sx+0.4, scope_sy+0.4 

rule cornice4:
    scale scope_sx+0.9, scope_sy+0.9 

rule pylon_top:
    split_z  
            ~1:  pylon_top3_pre |
            ~1:  pylon_top2_pre |
            ~1:  pylon_top1_pre  

rule pylon_top1_pre:
    split_x
            1.0:  pylon_top1_obelisk_block |
             ~1:  pylon_top1 |
            1.0:  pylon_top1_obelisk_block    

rule pylon_top1:
    scale  (scope_sx+2*1.0)*0.6, scope_sy*0.6 

rule pylon_top1_obelisk_block:
    rotate_scope 90 
    split_x    1:  pylon_top1_obelisk |
              ~10:  NIL |
               1:  pylon_top1_obelisk    

rule pylon_top1_obelisk:
    scale scope_sx, scope_sy, 1.8 
    create_roof skillion, 1.75 
    roof_direction  211.704 # could it be 45? and should be properly rotated

rule pylon_top2_pre:
    scale scope_sx*0.5, scope_sy*0.5 

rule pylon_top3_pre:
    scale scope_sx*0.4, scope_sy*0.4 
 

rule arch_columns:
    split_x 
            ~0.2:  semi_column_block |   ~1.2:  NIL |
              ~1:  arch_column_block |   ~1.1:  NIL |
              ~1:  arch_column_block |   ~1.1:  NIL |
              ~1:  arch_column_block |   ~1.1:  NIL |
              ~1:  arch_column_block |   ~1.1:  NIL |
              ~1:  arch_column_block |   ~1.1:  NIL |
              ~1:  arch_column_block |   ~1.2:  NIL |
            ~0.2:  semi_column_block    

rule arch_column_block:
    rotate_scope 90 
    # split x { {~sy:porch_column_pre| ~sy:Nil}* | ~sy:porch_column_pre }
    split_x    ~1:  porch_column_pre |
             ~0.2:  NIL |
               ~1:  porch_column_pre |
             ~1.1:  NIL |
               ~1:  porch_column_pre |
             ~0.2:  NIL |
               ~1:  porch_column_pre    

    # let's hope the grammar will allow such trick,
    # and the current shape is still the same, so we can split it twice
    split_x     ~1:  obelisk_pre |
                ~8:  NIL |
                ~1:  obelisk_pre    


rule porch_column_pre:
    split_z 0.25: porch_column_top | ~1: porch_column_main   

rule porch_column_top:
    #top_size = min(scope_sx, scope_sy )   / 1.0
    #align_scope x_to_longer_side
    scale scope_sx, scope_sx 

rule porch_column_main:
    primitive_cylinder scope_sx / 3  # min(ctx.scope_sx(), ctx.scope_sy()) / 3

rule semi_column_block:
    rotate_scope 90 

    split_x     ~1:  semi_column_pre |
              ~0.2:  NIL |
                ~1:  semi_column_pre |
              ~1.1:  NIL |
                ~1:  semi_column_pre |
              ~0.2:  NIL |
                ~1:  semi_column_pre    

rule semi_column_pre:
    split_z  0.25:  abacus |  ~1:  fustis    

rule abacus:
    scale scope_sx, scope_sy+0.5 

rule fustis:
    scale scope_sx-0.5, scope_sy   

rule obelisk_pre:
    
    translate 0, 0, 18.1
    scale 1, 1.5, 1.8 
    #tag  min_height,  18.3  
    #tag  height,  20.1  
    create_roof round,  1.70  
    tag  roof:orientation,  across  
    
rule stilobate:
    nope
    
rule pylon_middle_wall:
    nope
    
rule pylon:
    nope

rule NIL:
    nil  