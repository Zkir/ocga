# OCGA rules example for osm-cga project
# Rossi Pavilion
# https://en.wikipedia.org/wiki/Rossi_Pavilion
# (c) Zkir 2024

ocga 0.1

const COLUMN_WIDTH=0.75

rule building:
    align_scope geometry
	rotate_scope -90
	tag height, 0.1
	outer_rectangle mass_model
	
rule mass_model:
	scale 24, 15, 8
	split_y ~5: rotunda | ~5: main_part
	
rule main_part: 	
	split_x 7.5: chamberL | ~1: middle_part | 7.5: chamberR
	
rule middle_part:
    tag window, no 
	scale scope_sx+2, '0.65	
	split_z 1.2: roof | 0.25: cornice| 0.75:entablature | ~6:collonade_pre | 1.2:stylobate
    comp_edges -2.5, steps_pre

rule roof:
    tag window, no
    split_z ~1: roof1 | ~8:roof2 | ~2:roof3
    
rule roof1:
    colour white
    scale scope_sx+0.2, scope_sy+0.2

rule roof2:
    nope

rule roof3:    
    colour white
	
rule cornice:
	colour white
    tag window, no
	scale scope_sx+1, scope_sy+1
	
rule entablature:
    tag window, no
	colour white
	
rule collonade_pre:
	split_y COLUMN_WIDTH:collonade | ~4:NIL | COLUMN_WIDTH:collonade
    
rule wall:
    tag window:center:shape, rectangle
	tag window:top:shape, semicircle
    tag window:width,   1.9
	tag	window:height,  3.25
	tag	window:breast,  0.7
    tag window:panes,   2x2
	#tag	window:shutter, both    

rule wall_border:
    colour white
    tag window, no
	
rule stylobate:
	colour grey
    tag window, no
	
rule rotunda:
    tag window, no 		
    scale 12, 6
    rotate_scope -90
	primitive_halfcylinder '1, 22	
	translate -2, 0, 0
	split_z 0.6: NIL| 0.6: roof | 0.25: cornice | 0.75:entablature | ~6:rotunda_collonade_pre | 1.2:stylobate
    comp_border -2.5, steps_collonade_pre
    #comp_edges -2, steps_collonade_pre
    
	
rule rotunda_collonade_pre:
	comp_edges COLUMN_WIDTH, rotunda_collonade	
    restore
    translate '-0.025, 0
    scale '0.95, '0.95
    comp_border 0.5, rotunda_wall

rule rotunda_collonade:
    if split_index%4==1 then
        continue half_column_pre 
    else
        nil    
    endif  

rule steps_collonade_pre:
    colour grey
    material stone
    roof_colour grey
    roof_material stone
    scale '1, '1, 1.2
    
    if split_index==0 then 
        nil
    else
        if split_index<=4 or split_index>=18  then
            
            split_y ~2: NIL  | ~1:rotunda_outer
        else    
            if split_index%4!=1 then
                split_y ~2: steps  | ~1:rotunda_outer
            else 
                split_y ~1:steps_side | ~1:steps_side | ~1:rotunda_outer   
            endif   
        endif     
    endif     

rule rotunda_wall:
    if split_index==0 then 
       nil
    endif
    
    if split_index%4==1 then
        nope
    endif     

    if split_index%4==2 then
        translate 0, 0, 3.5
        scale '1, '1, 1.5
    endif 
    
    if split_index%4==3 then
        translate 0, 0, 4.0
        scale '1, '1, 1.5
    endif 
    
    if split_index%4==0 then
        translate 0, 0, 3.5
        scale '1, '1, 1.5
    endif 
    
                
	
rule chamberL:
    comp_edges 1, porticoL	
	restore
	scale scope_sx-2, scope_sy-2
	split_z 1.2: roof | 0.25: cornice | 0.75:entablature | ~6:wall | 0.3: wall_border | 1.2:stylobate
    
    
rule chamberR:    
    comp_edges 1, porticoR	
	restore
	scale scope_sx-2, scope_sy-2
	split_z 1.2: roof | 0.25: cornice | 0.75:entablature | ~6:wall | 0.3: wall_border |  1.2:stylobate
    
	
rule porticoL:
    # there are porticos on front and outer sides
    
    if split_index == 1  then
        continue small_portico
    endif    
    
    if split_index == 2  then
        nil
    endif    
    
    if split_index == 3  then
        continue portico
    endif    
    
    if split_index == 0  then
        continue portico
    endif    
    
rule porticoR:
    # there are porticos on front and outer sides
    if split_index == 0  then
        nil
    endif        
    
    if split_index == 1  then
        continue small_portico
    endif    
    
    if split_index == 2  then
        continue portico
    endif    
    
    if split_index == 3  then
        continue portico
    endif    
    
    
    
    
    
    
rule portico:    
    scale '0.45, '1
    split_z 1.2: portico_roof| 0.25: cornice|0.75:entablature|~6:collonade|1.2:stylobate
    
rule small_portico:    
    translate    0, '0.35
    scale     '0.45, '0.3
    split_z 1.2: roof | 0.25: cornice | 0.75:entablature | ~6:small_collonade | 1.2:stylobate

rule portico_roof:
	scale '1.075, '1, '0.8
	create_roof gabled, '0.95
	tag roof:orientation, across

rule collonade:
	split_x (COLUMN_WIDTH: column_pre | ~2.75 : NIL )* | COLUMN_WIDTH: column_pre 
    
    
rule small_collonade:    
    # there are flat half columns on the back side!
    split_x (COLUMN_WIDTH: half_column_pre | ~2.5 : NIL )* | COLUMN_WIDTH: half_column_pre 

rule column_pre:
	colour white
    split_z 0.10: abacus | ~1: column_fust 
    
rule half_column_pre:  
    translate  0, '0.1  
    scale '0.5, '0.8  
    colour white
    split_z 0.10: abacus | ~1: half_column_fust 

    
rule abacus:
    scale scope_sx+0.3, scope_sy+0.3
    #translate 0, -0.1
    
rule column_fust:
    primitive_cylinder '0.9
    
rule half_column_fust:
    nope
   
    
rule steps_pre:
    colour grey
    material stone
    roof_colour grey
    roof_material stone
    
    if split_index==3 then
        scale '1, '1, 1.2
        split_x COLUMN_WIDTH: steps_side_pre  |~1: steps | COLUMN_WIDTH: steps_side_pre
    else
        nil
    endif    

rule rotunda_outer:
    nope    

rule steps:
    create_roof skillion, '0.9
    roof_direction 180
    
rule steps_side_pre:
    split_y ~1:steps_side | ~1:steps_side    

rule steps_side:
    scale '1, '1, scope_sz*(split_index+1)/2   

rule NIL:
	nil

