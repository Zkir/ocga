# OCGA rules example for osm-cga project
# Komsomolskaya metro station
# https://ru.wikipedia.org/wiki/%D0%9A%D0%BE%D0%BC%D1%81%D0%BE%D0%BC%D0%BE%D0%BB%D1%8C%D1%81%D0%BA%D0%B0%D1%8F_(%D1%81%D1%82%D0%B0%D0%BD%D1%86%D0%B8%D1%8F_%D0%BC%D0%B5%D1%82%D1%80%D0%BE,_%D0%9A%D0%BE%D0%BB%D1%8C%D1%86%D0%B5%D0%B2%D0%B0%D1%8F_%D0%BB%D0%B8%D0%BD%D0%B8%D1%8F)#%D0%92%D0%B5%D1%81%D1%82%D0%B8%D0%B1%D1%8E%D0%BB%D1%8C
# (c) Zkir 2024
ocga 0.1

rule building:
    align_scope geometry
	rotate_scope 90
	outer_rectangle mass_model
	#tag height, 0.1
	
rule mass_model:
    tag windows, no
	scale '1, '1,  13
	split_x ~10.5: side_part1 | ~43:main_part | ~10.5:side_part2
	
rule main_part:
	scale '1, '1, 47
	split_z   ~31: roofs_and_spire | 0.1:roof0 | 13: wall_block 
	
rule side_part1:
	scale '1, '0.55
	translate 0.5, 0
	split_x ~1: portico | ~1: wall_block		
	
rule side_part2:
    # we definetly need IF operator 
	# if split_index=3:
	 	rotate_scope 180
	# endif 
	scale '1, '0.55
	translate 0.5, 0
	split_x ~1: portico | ~1: wall_block		

rule roofs_and_spire: 	
    colour white
	material plaster
	roof_colour gray
	roof_material metal
    primitive_cylinder '0.75, 8
	rotate 22.5 # we need the dome face to look front
	split_z 12: spire |
               2: baraban_dome| 0.3: baraban_dome_cornice| 3: baraban |0.3: baraban_cornice|
               ~3: dome_top |0.1: dome_middle |~6: dome_bottom | 1: dome_base |2.25: dome_slides | 3:slide_base 
	
rule spire:
	scale 0.6, 0.6
	translate 0,0, -0.5
	create_roof pyramidal, '0.75
	colour slategray 
	material metal
	roof_colour slategray 
	roof_material metal
	
rule baraban_dome:
    scale 4, 4
    create_roof dome, '0.99
	
rule baraban_dome_cornice:
	scale 4.75, 4.75
	
rule baraban:
	scale 4, 4
	
rule baraban_cornice:
	scale 5, 5
	
rule dome_top:
    scale 9, 9,scope_sz+5
	translate 0,0, -1
	create_roof pyramidal, '0.99

rule dome_middle: 	
	scale 12, 12, scope_sz+4.5
	translate 0,0, -1.25
	create_roof pyramidal, '0.99
	
rule dome_bottom:
    scale '0.8, '0.8
	create_roof dome, '1
	
rule dome_base:
	scale scope_sx-4.75, scope_sy-4.75
	
rule dome_slides:
	split_z ~3: dome_slides1 | ~2: dome_slides2
	restore
	scale scope_sx-3.7, scope_sy-3.7
	
rule dome_slides1:	
	scale scope_sx-2.2, scope_sy-2.2
	comp_border 0.81, dome_slide
	
rule dome_slides2:	
	scale scope_sx+0.5, scope_sy+0.5
	comp_border 1.46, dome_slide	


rule dome_slide:
	create_roof skillion, '1
	roof_direction 180
	
rule slide_base: 	
	nope 
	
rule roof0:
    colour white
	material plaster
	roof_colour gray
	roof_material metal
    #scale scope_sx,scope_sx
	scale '0.9, '0.9, 3
	comp_border 2, dormers_pre
	restore
	create_roof pyramidal, '0.99 
	
	
rule dormers_pre:
    if split_index%2==0 then 
        split_x ~1:NILL| 2:dormer | ~2.25:NILL | 2:dormer | ~1:NILL
    else
        nil
    endif
	
rule dormer:
    translate 0, 4.5+0.75, 0.3 
    scale '1, 5.0, scope_sx/1.75
    create_roof round, '0.99
    tag roof:orientation, along  	
	
rule wall_block:
    split_z 4: entablement | ~1: wall | 0.6: stylobate 
	
rule wall:
    colour tan
    material plaster	
    scale scope_sx-0.5, scope_sy-0.5
	
rule portico:
    split_z 4: entablement | ~1: collonade_pre  
	
rule entablement:
    colour white
    material plaster
	split_z ~2:fries  | 0.4: cornice | ~3: architrave
	
rule cornice: 
    scale scope_sx+1.5, scope_sy+1.5	
	
rule fries: 
    nope
	#we cannot do this, because geometry will be separated
	#scale scope_sx-0.5, scope_sy-0.5	
	
rule collonade_pre:
	split_x ~1.5: collonade | ~2: NILL		
	
rule collonade:
    colour lightgray
    material marble # ???
    # we can use repetion in the split pattern ()* 			 
	# the interval in the middle is larger
	split_y  (1.95: column | ~3.0: NILL)* |  1.95: column | ~4: NILL | (1.95: column | ~3.0: NILL )* |  1.95: column     		 
			 
rule column:
	scale scope_sy, scope_sy
	split_z 0.15:abacus | 0.4:echinus1 | 0.4:echinus2 | 0.4:echinus3 |  ~11: column_fust | 0.3: column_base | 0.6: stylobate 
	
rule abacus:	
	scale '1.3, '1.3

rule echinus1:
	primitive_cylinder '1	
	
rule echinus2:
	primitive_cylinder '0.85
	
rule echinus3:
	primitive_cylinder '0.75	

rule column_fust:	
	primitive_cylinder '0.7, 8  
    scale '1, '1, scope_sz+0.3
    translate 0, 0, -0.3	
	
rule column_base:	
    primitive_cylinder '0.95
	create_roof dome, '0.95
	
rule stylobate: 	
	#scale scope_sx-2, scope_sy-2
	colour brown
	material granite
    roof_colour brown
	roof_material granite	

rule architrave:
    nope    
	
rule NILL:
    nil 