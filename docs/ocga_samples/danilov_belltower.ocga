# OCGA rules example for osm-cga project
#see https://github.com/Zkir/ocga
#
# Danilov Monastery Belltower
# https://ru.wikipedia.org/wiki/%D0%94%D0%B0%D0%BD%D0%B8%D0%BB%D0%BE%D0%B2_%D0%BC%D0%BE%D0%BD%D0%B0%D1%81%D1%82%D1%8B%D1%80%D1%8C#/media/%D0%A4%D0%B0%D0%B9%D0%BB:%D0%AF%D1%80%D1%83%D1%81%D0%BD%D0%B0%D1%8F_%D1%86%D0%B5%D1%80%D0%BA%D0%BE%D0%B2%D1%8C-%D0%BA%D0%BE%D0%BB%D0%BE%D0%BA%D0%BE%D0%BB%D1%8C%D0%BD%D1%8F_%D0%A1%D0%B8%D0%BC%D0%B5%D0%BE%D0%BD%D0%B0_%D0%A1%D1%82%D0%BE%D0%BB%D0%BF%D0%BD%D0%B8%D0%BA%D0%B0_%D0%BD%D0%B0%D0%B4_%D0%A1%D0%B2%D1%8F%D1%82%D1%8B%D0%BC%D0%B8_%D0%B2%D0%BE%D1%80%D0%BE%D1%82%D0%B0%D0%BC%D0%B8_%D0%94%D0%B0%D0%BD%D0%B8%D0%BB%D0%BE%D0%B2_%D0%BC%D0%BE%D0%BD%D0%B0%D1%81%D1%82%D1%8B%D1%80%D1%8C.jpg
#(c) Zkir 2026

ocga 0.1

rule building:

    align_scope geometry
	
	# define the buidling height expicitly, 
	# overwriting what is specified in the source file
	tag height, 45

    # we will start from the rectangle and will rebuild the form
    # basing of this very algorithm
    outer_rectangle mass_model
    
	
	
rule mass_model:
    split_z 
		7:   onion    |
		4.5:   octagon3 |
		6:   octagon2 |
		10:  octagon1 | 
		8:   middle   |
		~1:  base


rule onion:
	scale scope_sy-1, scope_sy-1
	scale '0.1, '0.1
	primitive_cylinder '1, 8
	rotate 22.5 
	create_roof onion, '0.65
	colour yellow
	roof_colour yellow

rule octagon3:
	scale scope_sy-1, scope_sy-1, scope_sz+2
	scale '0.18, '0.18
	primitive_cylinder '1, 8
	rotate 22.5 
	split_z 1.5: octagon_roof | ~3: octagon3a
	
rule octagon3a:	
    insert2 '0.2, -0.3	

rule octagon2:
	scale scope_sy-1, scope_sy-1,  scope_sz+2
	scale '0.4, '0.4
	primitive_cylinder '1, 8
	rotate 22.5 
	split_z 2: octagon_roof | ~3: octagon2a

rule octagon2a:	
    insert2 '0.2, -0.3

rule octagon1:
	scale scope_sy-1, scope_sy-1, scope_sz+2
	primitive_cylinder '0.8, 8
	rotate 22.5 
	split_z 4: octagon_roof | ~3: octagon1a
	
	
rule octagon1a:	
    insert2 '0.2, -0.4
	
rule octagon_roof:
    create_roof dome, '1
	
rule middle:
    split_x ~1:side | scope_sy: cube | ~1:side 	

rule cube:
	scale scope_sy-1, scope_sy-1, scope_sz+2.5
	create_roof pyramidal, 3
	insert2 '0.2, -0.3, [0,2]

	
rule side:
    if relative_Ox > 0 then 
        rotate_scope 180 
    endif
    scale '1, scope_sy-1 , '0.8
	create_roof side_hipped, 2
	#tag roof:orientation, across
	roof_direction 270
	translate 0.5, 0, 0
	
	
rule base_pre:
    split_y 0.75: base_columns | ~1: base | 0.75: base_columns

rule base_columns:
    split_x 2: base_column | ~3: NIL | 2: base_column | ~4: NIL | 2: base_column | ~3: NIL | 2: base_column
	
rule NIL:
	nil
	
rule base_column:
	colour white
