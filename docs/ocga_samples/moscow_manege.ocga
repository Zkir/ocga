# OCGA rules example for osm-cga project
#see https://github.com/Zkir/ocga
#
# Moscow Manage
# https://en.wikipedia.org/wiki/Moscow_Manege
#(c) Zkir 2024

ocga 0.1

rule building:

    align_scope geometry
	
	# define the buidling height expicitly, 
	# overwriting what is specified in the source file
	tag height, 24

    # we will start from the rectangle and will rebuild the form
    # basing of this very algorithm
    outer_rectangle mass_model
    #massModel mass_model
	
	
rule mass_model:
    split_z 10:roof | 2.25: entablature | ~13: main_part 
	
rule roof: 	
	create_roof gabled, scope_sz-0.40
	colour white
	roof_material metal
	roof_colour darkgreen
	
rule entablature:
	scale scope_sx-3, scope_sy-3
	split_z ~2: fries | ~1:  architrave

rule fries:
	scale scope_sx+0.1, scope_sy+0.1
	colour white
	
rule main_part:
	scale scope_sx-2.5, scope_sy-2.5
	comp_edges 1.5, colonade
	restore
	scale scope_sx-1*2, scope_sy-1*2
	
rule colonade:
    # we can use repetion in the split pattern ()* 			 
	split_x  (1.5: column_pre | ~4.5: wall_block)* |
              1.5: column_pre | ~4.5: door_block | 1.5: column_pre | ~4.5: door_block| 1.5: column_pre | ~4.5: door_block |
			 (1.5: column_pre | ~4.5: wall_block)* | 1.5:NIL   		 
  		 
			 
rule column_pre:
    colour white
	roof_colour white
	split_z 0.3:abacus| 0.2:echinus |  ~11: column | 0.3: abacus | 2: stylobate 
	
rule wall_block:
	split_z ~11.5: NIL | 2.3: stylobate2 
	
rule column:	
	primitive_cylinder '0.7  
	
rule abacus:	
	scale '0.95, '0.95
	
rule echinus:
	primitive_cylinder '0.85
	
rule stylobate: 	
	#scale scope_sx-2, scope_sy-2
	colour grey
	material granite
	
rule stylobate2: 	
	colour grey
	material granite		
	roof_colour grey
	roof_material metal		
	create_roof skillion, 0.3
	#roof_direction 16-90
	roof_direction 180 #magic. direction is in local coordinate system, so it is the same for all parts!
	
rule door_block:
	colour maroon
	roof_colour maroon
	material wood
	scale '0.8, '0.1, '0.9
	create_roof round, scope_sx/2
	tag roof:orientation, across
	translate 0, 0.25, 0

rule architrave:
    nope    
	
rule NIL:
	nil