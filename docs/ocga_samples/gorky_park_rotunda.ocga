#OCGA rules example
#Gorky Park Rotunda
#(c) Zkir 2020

ocga 0.1

rule building:
    if scope_sz == 0 then
        scale '1, '1, scope_sx*1.3
    endif     
    massModel mass_model

rule mass_model:
    colour  &B0B0B0
    material plaster

    split_z  ~5:  roof |
            ~0.3: cornice| 
            ~1.5: entablement|
            ~9.2: collonade|
             0.6: stilobate

rule stilobate:
    roof_colour   &101010
    roof_material stone
    colour        &202020
    material      stone

rule roof:
    scale '0.9, '0.9
    roof_colour brown
    roof_material metal
    colour brown
    material  metal
    split_z ~0.3: roof4 |
            ~2.5: dome |
            ~0.5: roof2 |
            ~0.9: roof1


rule collonade:
    scale '0.9, '0.9
    comp_edges size/8, column_pre

rule entablement:
    scale '0.9, '0.9 

rule cornice:
    nope

rule roof1:
    nope

rule roof2:
    scale '0.9, '0.9 

rule dome:
    scale '0.8, '0.8 
    create_roof dome, scope_sz-0.01

rule roof4:
    scale '0.25, '0.25, '2 
    translate 0, 0, '-0.5 

rule column_pre:
    translate 0, '-0.1 
    scale scope_sy, '1 
    scale '1.2, '1.2 
    if split_index%2 == 0 then
        split_z scope_sx/6:column_top1 | ~1: column_trunk_pre
    else
        nil
    endif    

rule column_trunk_pre:
    primitive_cylinder '1 
    split_z scope_sx/10: column_top2 | ~1: column_trunk

rule column_trunk:
    scale '0.8,'0.8 
    
rule column_top1:
    nope
    
rule column_top2:
    nope
